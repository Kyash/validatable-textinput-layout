import dependencies.Depends
import dependencies.Versions

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 0

group = 'co.kyash'
version = "$versionMajor.$versionMinor.$versionPatch"

android {
    compileSdkVersion Versions.androidCompileSdkVersion

    defaultConfig {
        minSdkVersion Versions.androidMinSdkVersion
        targetSdkVersion Versions.androidTargetSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "$versionMajor.$versionMinor.$versionPatch"
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = "validatable-textinput-layout"
                artifact(sourcesJar)
            }
        }
    }
}

dependencies {
    //==================== Support Library ====================
    implementation Depends.AndroidX.appCompat
    implementation Depends.AndroidX.design

    //==================== Structure ====================
    implementation Depends.Rx.RxJava

    //==================== Test ====================
    testImplementation Depends.Test.junit
    testImplementation Depends.Test.mockitoKotlin
    testImplementation Depends.Test.robolectric
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}
